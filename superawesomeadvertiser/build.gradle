apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

def siteUrl = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-advertiser-android'
def gitUrl = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-advertiser-android.git'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    resourcePrefix "superawesome__"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SuperAwesomeAdvertiser"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SuperAwesome Advertiser Mobile SDK for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gabrielcoman'
                        name 'Gabriel Coman'
                        email 'gabriel.coman@superawesome.tv'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

repositories {
    maven {
        url  "http://dl.bintray.com/gabrielcoman/maven"
    }
}

dependencies {
    api 'tv.superawesome.lib.sanetwork:sanetwork:1.2.0'
    api 'tv.superawesome.lib.samodelspace:samodelspace:1.0.1'
    testCompile 'junit:junit:4.12'
}
